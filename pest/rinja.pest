// replace it later with passing in params using tmpl?
Lexpr = _{"{{"}
Rexpr = _{"}}"}
Lcmt = _{"{#"}
Rcmt = _{"#}"}
Lstmt = _{ "{%"}
Rstmt = _{"%}"}
lnstmt = _{"##"}

WHITESPACE = _{ " " }
keywords = {"for"|"endfor"|"if"|"else"|"endif"|"in"|"set"}
// expr & stmt & comment
alpha = _{ 'a'..'z' | 'A'..'Z' }
digit = _{ '0'..'9' }
ident = @{ !keywords ~ alpha ~ (alpha | digit)* }
operation = _{ add | subtract | multiply | divide | power | shift | cmp }
    add      = { "+" }
    subtract = { "-" }
    multiply = { "*" }
    divide   = { "/" }
    power    = { "^" }
    shift   = { "<<" | ">>" }
    cmp      = { "<=" | ">=" | "==" | "!=" | "<" | ">" }
// for now escape_char is just a 
escape_char = { "\\" ~ ((&"x" ~ "x" ~ ANY ~ ANY)|ANY) }
str = {"\"" ~ 
    (!"\"" ~ 
        (
            (&escape_char ~  ANY)
            | ANY
        )
    )* 
    ~ "\""
    }
num = @{ int ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ int)? }
    int = { ("+" | "-")? ~ ASCII_DIGIT+ }
subs = {(ident ~ "." ~ ident) | (ident ~ "[" ~ str ~ "]")}
expr = { term ~ (operation ~ term)* }
term = _{ subs | ident | num |  "(" ~ expr ~ ")" }

// all the statement
if_stmt = { Lstmt ~ "if" ~ expr ~ Rstmt
| lnstmt ~ "if" ~ expr ~ "\n"
}
// else if
else_if_stmt = { Lstmt ~ "else" ~ "if" ~ expr ~ Rstmt
| lnstmt ~ "else" ~ "if" ~ expr ~ "\n"
}
// else
else_stmt = { Lstmt ~ "else" ~ Rstmt
| lnstmt ~ "else" ~ "\n"}
// only endif/for and set_stmt can append by End of Input
endif = _{Lstmt ~ "endif" ~ Rstmt
| lnstmt ~ "endif" ~ ("\n"|&EOI)}
for_stmt = { Lstmt ~ "for" ~ ((ident ~ "," ~ ident)|ident) ~ "in" ~ expr ~ Rstmt
| lnstmt ~ "for" ~ ((ident ~ "," ~ ident)|ident) ~ "in" ~ expr ~ "\n"}
endfor = _{Lstmt ~ "endfor" ~ Rstmt
| lnstmt ~ "endfor" ~ ("\n"|&EOI)}
set_stmt = {Lstmt ~  "set" ~ expr ~ "=" ~ expr ~ Rstmt
| lnstmt ~ "set" ~ expr ~ "=" ~ expr ~ ("\n"|&EOI)}

//wrap means their wrapping deilmeter is also parsed
wrap_comment = _{Lcmt ~ (!Rcmt ~ ANY)* ~ Rcmt}
wrap_line_comment = _{"##" ~ (!"\n" ~ ANY)* ~ "\n"}


// multiple or nested statement construct a 'block'
if_body = {if_stmt ~ tmpl_section?}
else_if_body = {else_if_stmt ~ tmpl_section?}
else_body = {else_stmt ~ tmpl_section?}
if_block = { if_body ~ 
            else_if_body* ~ 
            else_body?
             ~ endif}
for_block = {for_stmt ~ tmpl_section? ~ endfor}
block = {if_block | for_block}

// to stop tmpl_literal
starting = { Lexpr | Lcmt | Lstmt | lnstmt}
// template literal any char other than starting symbol of tmpl var/expr/stmt
tmpl_literal = @{(!starting ~ ANY)+}
tmpl_section = _{block ~ tmpl_section
| Lexpr ~ expr ~ Rexpr ~ tmpl_section 
| set_stmt ~ tmpl_section
| wrap_comment ~ tmpl_section
| wrap_line_comment ~ tmpl_section
| tmpl_literal}
tmpl_unit = _{ SOI ~ tmpl_section? ~ EOI}

